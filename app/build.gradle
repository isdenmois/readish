plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.isdenmois.readish"
        minSdk 28
        targetSdk 31
        versionCode 24
        versionName "1.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        exclude 'META-INF/*'
        // Due to https://github.com/Kotlin/kotlinx.coroutines/issues/2023
        exclude 'META-INF/licenses/*'
        exclude '**/attach_hotspot_windows.dll'
        exclude '**/jansi.dll'
        exclude 'META-INF/*.kotlin_module'
        exclude '**.kotlin_builtins'
        exclude '**.kotlin_metadata'

        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
            excludes += '/org/fusesource**'
        }
    }
}

dependencies {
    implementation AndroidX.core.ktx
    implementation AndroidX.compose.ui
    implementation AndroidX.compose.material
    implementation AndroidX.compose.ui.toolingPreview
    implementation AndroidX.lifecycle.runtimeKtx
    implementation AndroidX.activity.compose

    implementation(Google.dagger.hilt.android) {
        exclude module: "dagger-lint-aar"
    }
    kapt Google.dagger.hilt.compiler
    implementation AndroidX.lifecycle.viewModelCompose
    implementation 'javax.annotation:javax.annotation-api:_'

    debugImplementation AndroidX.compose.ui.tooling

    implementation(project(':shared'))
    implementation(project(":feature:home"))
    implementation(project(":feature:transfers"))
}

configurations {
    implementation {
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-reflect'
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk7'
        exclude group: 'androidx.lifecycle', module: 'lifecycle-livedata-core'
        exclude group: 'androidx.lifecycle', module: 'lifecycle-livedata'
    }
}
